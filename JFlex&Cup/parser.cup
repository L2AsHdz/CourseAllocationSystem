package courseallocationsystem.analizadores;

import courseallocationsystem.edd.list.CircularList;
import courseallocationsystem.edd.list.List;
import courseallocationsystem.edd.table.HashTable;
import courseallocationsystem.edd.tree.ArbolAVL;
import courseallocationsystem.edd.tree.BTree;
import courseallocationsystem.model.Asignacion;
import courseallocationsystem.model.Catedratico;
import courseallocationsystem.model.Curso;
import courseallocationsystem.model.Edificio;
import courseallocationsystem.model.Estudiante;
import courseallocationsystem.model.Horario;
import courseallocationsystem.model.Salon;
import courseallocationsystem.model.Usuario;
import courseallocationsystem.validator.AsignacionValidator;
import courseallocationsystem.validator.CatedraticoValidator;
import courseallocationsystem.validator.CursoValidator;
import courseallocationsystem.validator.EdificioValidator;
import courseallocationsystem.validator.EstudianteValidator;
import courseallocationsystem.validator.HorarioValidator;
import courseallocationsystem.validator.SalonValidator;
import courseallocationsystem.validator.UserValidator;
import java.util.ArrayList;
import java_cup.runtime.Symbol;

parser code {:

    private ArrayList<String> errores = new ArrayList();

    private CircularList<Usuario, Integer> usuarios = new CircularList();
    private CircularList<Edificio, String> edificios = new CircularList();
    private CircularList<Curso, Integer> cursos = new CircularList();
    private HashTable<Estudiante, Integer> estudiantes = new HashTable(37, 0.55f);
    private ArbolAVL<Catedratico, Integer> catedraticos = new ArbolAVL();
    private BTree<Horario, Integer> horarios = new BTree();

    public ArrayList<String> getErrores(){
        return this.errores;
    }

    public CircularList<Usuario, Integer> getUsuarios() {
        return this.usuarios;
    }

    public CircularList<Edificio, String> getEdificios() {
        return this.edificios;
    }

    public CircularList<Curso, Integer> getCursos() {
        return this.cursos;
    }

    public HashTable<Estudiante, Integer> getEstudiantes() {
        return this.estudiantes;
    }

    public ArbolAVL<Catedratico, Integer> getCatedraticos() {
        return this.catedraticos;
    }

    public BTree<Horario, Integer> getHorarios() {
        return this.horarios;
    }

    public void syntax_error(Symbol s) {
        String ss =  (String) s.value;
        StringBuilder descripcion = new StringBuilder("Se esperaba: ");
        expected_token_ids().forEach(x -> descripcion.append(symbl_name_from_id(x)).append(", "));
        errores.add("Error en :" + ss + ", " + descripcion);
    }

    public void unrecovered_syntax_error(Symbol cur_token){
        String t = (String) cur_token.value;
        System.out.println("Error irrecuperable " + t);
    }

:}

terminal                USUARIO, ESTUDIANTE, EDIFICIO, SALON, CATEDRATICO, CURSO, HORARIO, ASIGNAR;
terminal String         USR_COLABORADOR, USR_ESTUDIANTE;
terminal                OPEN_ROUND_BRACKET, CLOSE_ROUND_BRACKET, COMMA, SEMI, QUOTE_MARK;
terminal String         LITERAL, ENTERO, NAME_ED;

non terminal            inicio, entidad, startEntidad, paramsUser, paramsEstudiante, paramsEdificio, paramsSalon;
non terminal            paramsCatedratico, paramsCurso, paramsHorario, paramsAsignar;
non terminal String     typeUser;

inicio
    ::= inicio entidad
    |   entidad
;

entidad
    ::= startEntidad CLOSE_ROUND_BRACKET SEMI
;

startEntidad
    ::= USUARIO OPEN_ROUND_BRACKET paramsUser
    |   ESTUDIANTE OPEN_ROUND_BRACKET paramsEstudiante
    |   EDIFICIO OPEN_ROUND_BRACKET paramsEdificio
    |   SALON OPEN_ROUND_BRACKET paramsSalon
    |   CATEDRATICO OPEN_ROUND_BRACKET paramsCatedratico
    |   CURSO OPEN_ROUND_BRACKET paramsCurso
    |   HORARIO OPEN_ROUND_BRACKET paramsHorario
    |   ASIGNAR OPEN_ROUND_BRACKET paramsAsignar
;

paramsUser
    ::= ENTERO:i COMMA QUOTE_MARK LITERAL:name QUOTE_MARK COMMA QUOTE_MARK LITERAL:pass QUOTE_MARK COMMA typeUser:t {:
            Integer id = Integer.parseInt(i);
            String error = UserValidator.validateUser(usuarios, estudiantes, id, t);
            if (error.isEmpty()) {
                usuarios.add(new Usuario(name, pass, t, id));
            } else {
                errores.add(error);
            }
        :}
;

typeUser
    ::= USR_COLABORADOR:t       {:RESULT = t;:}
    |   USR_ESTUDIANTE:t        {:RESULT = t;:}
;
paramsEstudiante
    ::= ENTERO:i COMMA QUOTE_MARK LITERAL:name QUOTE_MARK COMMA QUOTE_MARK LITERAL:dir QUOTE_MARK {:
            Integer id = Integer.parseInt(i);
            String error = EstudianteValidator.validateEstudiante(estudiantes, id);
            if (error.isEmpty()) {
                estudiantes.add(new Estudiante(name, dir, id));
            } else {
                errores.add(error);
            }
        :}
;

paramsEdificio
    ::= QUOTE_MARK LITERAL:name QUOTE_MARK {:
            String error = EdificioValidator.validateEdificio(edificios, name);
            if (error.isEmpty()) {
                edificios.add(new Edificio(name));
            } else {
                errores.add(error);
            }
        :}
;

paramsSalon
    ::= QUOTE_MARK LITERAL:s QUOTE_MARK COMMA ENTERO:i COMMA ENTERO:c {:
            Integer id = Integer.parseInt(i);
            String error = SalonValidator.validateSalon(edificios, s, id);
            if (error.isEmpty()) {
                edificios.get(s).getSalones().add(new Salon(Integer.parseInt(c), s, id));
            }
        :}
;

paramsCatedratico
    ::= ENTERO:i COMMA QUOTE_MARK LITERAL:name QUOTE_MARK COMMA QUOTE_MARK LITERAL:dir QUOTE_MARK {:
            Integer id = Integer.parseInt(i);
            String error = CatedraticoValidator.validateCatedratico(catedraticos, id);

            if (error.isEmpty()) {
                catedraticos.add(new Catedratico(name, dir, id));
            } else {
                errores.add(error);
            }
        :}
;

paramsCurso
    ::= ENTERO:i COMMA QUOTE_MARK LITERAL:name QUOTE_MARK COMMA ENTERO:s COMMA ENTERO:n {:
            Integer id = Integer.parseInt(i);
            String error = CursoValidator.validateCurso(cursos, id);
            if (error.isEmpty()) {
                cursos.add(new Curso(name, Integer.parseInt(s), Integer.parseInt(n), id));
            } else {
                errores.add(error);
            }
        :}
;

paramsHorario
    ::= ENTERO:i COMMA QUOTE_MARK LITERAL:p QUOTE_MARK COMMA QUOTE_MARK LITERAL:d QUOTE_MARK COMMA ENTERO:iCur COMMA ENTERO:idS COMMA NAME_ED:iEd COMMA ENTERO:iCated {:
            Integer id = Integer.parseInt(i);
            Integer idCurso = Integer.parseInt(iCur);
            Integer idCatedra = Integer.parseInt(iCated);
            Integer idSalon = Integer.parseInt(idS);
            String error = HorarioValidator.validateHorario(horarios, cursos, edificios, catedraticos, id, idCurso, idCatedra, idSalon, iEd);
            if (error.isEmpty()) {
                horarios.add(new Horario(p, d, idCurso, idSalon, iEd, idCatedra, id));
            } else {
                errores.add(error);
            }
        :}
;

paramsAsignar
    ::= ENTERO:iEst COMMA ENTERO:iHo COMMA ENTERO:z COMMA ENTERO:f {:
            Integer idEstudiante = Integer.parseInt(iEst);
            Integer idHorario = Integer.parseInt(iHo);
            String error = AsignacionValidator.validateAsignacion(horarios, idEstudiante, idHorario);
            if (error.isEmpty()) {
                horarios.get(idHorario).getAsignaciones().add(new Asignacion(idEstudiante, idHorario, Integer.parseInt(z), Integer.parseInt(f)));
            } else {
                errores.add(error);
            }
        :}
;